unit Mapping.CodeGenerator;

interface

uses
  Mapping.CodeGenerator.Abstract
  ,SysUtils
  ;

type
  TDelphiUnitCodeGenerator = class(TAbstractCodeGenerator)
  private
    FIntfBuilder: TStringBuilder;
    FImplBuilder: TStringBuilder;
    FUnitPrefix: string;
    FUseNullableTypes: Boolean;
  protected
    function AddTypeAttribute(const AAtributeText: string): TStringBuilder; virtual;
    function AddClassAttribute(const AAtributeText: string): TStringBuilder; virtual;

    function AddUnit(const AUnitName: string): TStringBuilder; virtual;
    function AddPrivateField(AColumnData: TColumnData): TStringBuilder; virtual;
    function AddPublicProperty(AColumnData: TColumnData): TStringBuilder; virtual;
    function AddEntityDeclaration(AEntityData: TEntityModelData): TStringBuilder; virtual;

    function GetPrivateFieldName(AColumnData: TColumnData): string; virtual;
    function GetColumnAttributeText(AColumnData: TColumnData): string; virtual;
    function GetColumnProperties(AColumnData: TColumnData): string; virtual;
    function GetColumnTypeName(AColumnData: TColumnData): string; virtual;

    function DoGenerate(AEntityData: TEntityModelData): string; override;
  public
    constructor Create(); virtual;
    destructor Destroy; override;

    function GetUnitName(AEntityData: TEntityModelData): string;

    function Generate(AEntityData: TEntityModelData): string;

    property UnitPrefix: string read FUnitPrefix write FUnitPrefix;
    property UseNullableTypes: Boolean read FUseNullableTypes write FUseNullableTypes;
  end;

implementation

const
  UNIT_ATTRIBUTES = 'Mapping.Attributes';
  UNIT_TYPES = 'Core.Types';
  UNIT_GRAPHICS = 'Graphics';

{ TEntityCodeGenerator }

function TDelphiUnitCodeGenerator.AddTypeAttribute(const AAtributeText: string): TStringBuilder;
begin
  Result := FIntfBuilder.AppendLine.Append(GetIndent).Append(AAtributeText);
end;

function TDelphiUnitCodeGenerator.AddClassAttribute(const AAtributeText: string): TStringBuilder;
begin
  Result := FIntfBuilder.AppendLine.Append(GetIndent).Append(GetIndent).Append(AAtributeText);
end;

function TDelphiUnitCodeGenerator.AddEntityDeclaration(AEntityData: TEntityModelData): TStringBuilder;
begin
  //add attribute
  AddTypeAttribute('[Entity]');
  AddTypeAttribute(Format('[Table(%0:S, %1:S)]', [QuotedStr(AEntityData.TableName), QuotedStr(AEntityData.SchemaName)]));

  FIntfBuilder.AppendLine.Append(GetIndent);

  Result := FIntfBuilder.Append(GetEntityTypePrefix).Append(AEntityData.TableName).Append(' = class');
end;

function TDelphiUnitCodeGenerator.AddPrivateField(AColumnData: TColumnData): TStringBuilder;
begin
  Result := FIntfBuilder.AppendLine.Append(GetIndent).Append(GetIndent).Append(GetPrivateFieldName(AColumnData))
    .Append(': ').Append(GetColumnTypeName(AColumnData)).Append(';');
end;

function TDelphiUnitCodeGenerator.AddPublicProperty(AColumnData: TColumnData): TStringBuilder;
begin
  if AColumnData.IsAutogenerated then
  begin
    AddClassAttribute('[AutoGenerated]');
  end;

  AddClassAttribute(GetColumnAttributeText(AColumnData));

  Result := FIntfBuilder.AppendLine.Append(GetIndent).Append(GetIndent).Append('property ')
    .Append(AColumnData.ColumnName).Append(': ').Append(GetColumnTypeName(AColumnData))
    .Append(' read ').Append(GetPrivateFieldName(AColumnData)).Append(' write ')
    .Append(GetPrivateFieldName(AColumnData)).Append(';');
  {TODO -oLinas -cGeneral : add implementation section if needed, e.g. for lazy types}
end;

function TDelphiUnitCodeGenerator.AddUnit(const AUnitName: string): TStringBuilder;
begin
  Result := FIntfBuilder.AppendLine.Append(GetIndent).Append(',').Append(AUnitName);
end;

constructor TDelphiUnitCodeGenerator.Create;
begin
  inherited Create;
  FIntfBuilder := TStringBuilder.Create;
  FImplBuilder := TStringBuilder.Create;
  FUnitPrefix := 'ORM.Model.';
end;

destructor TDelphiUnitCodeGenerator.Destroy;
begin
  FIntfBuilder.Free;
  FImplBuilder.Free;
  inherited Destroy;
end;

function TDelphiUnitCodeGenerator.DoGenerate(AEntityData: TEntityModelData): string;
var
  LColumnData: TColumnData;
begin
  FIntfBuilder.Clear;
  FImplBuilder.Clear;

  FIntfBuilder.AppendFormat('unit %S;', [GetUnitName(AEntityData)]).AppendLine.AppendLine;

  FIntfBuilder.Append('interface').AppendLine.AppendLine;
  FImplBuilder.AppendLine.AppendLine.Append('implementation').AppendLine;

  FIntfBuilder.Append('uses').AppendLine.Append(GetIndent).Append(UNIT_ATTRIBUTES);
  AddUnit(UNIT_TYPES);
  AddUnit(UNIT_GRAPHICS);
  FIntfBuilder.AppendLine.Append(GetIndent).Append(';');

  FIntfBuilder.AppendLine.AppendLine;

  FIntfBuilder.Append('type');

  AddEntityDeclaration(AEntityData);

  //add private fields
  FIntfBuilder.AppendLine.Append(GetIndent).Append('private');

  for LColumnData in AEntityData.Columns do
  begin
    AddPrivateField(LColumnData);
  end;

  //add public properties

  FIntfBuilder.AppendLine.Append(GetIndent).Append('public');

  for LColumnData in AEntityData.Columns do
  begin
    AddPublicProperty(LColumnData);
  end;

  FIntfBuilder.AppendLine.Append(GetIndent).Append('end').Append(';');

 // FIntfBuilder.AppendLine.AppendLine.Append('implementation');

  FImplBuilder.AppendLine.AppendLine.Append('end').Append('.');

  Result := FIntfBuilder.ToString + FImplBuilder.ToString;
end;

function TDelphiUnitCodeGenerator.Generate(AEntityData: TEntityModelData): string;
begin
  Result := DoGenerate(AEntityData);
end;

function TDelphiUnitCodeGenerator.GetColumnAttributeText(AColumnData: TColumnData): string;
var
  LBuilder: TStringBuilder;
begin
  LBuilder := TStringBuilder.Create;
  try
    LBuilder.Append('[Column');
    LBuilder.Append('(');

    LBuilder.Append(QuotedStr(AColumnData.ColumnName));
    LBuilder.Append(',');
    LBuilder.Append(GetColumnProperties(AColumnData));

    if AColumnData.ColumnLength.HasValue then
      LBuilder.Append(',').Append(AColumnData.ColumnLength.Value);

    if AColumnData.ColumnPrecision.HasValue then
    begin
      LBuilder.Append(',').Append(AColumnData.ColumnPrecision.Value);
      LBuilder.Append(',').Append(AColumnData.ColumnScale.GetValueOrDefault(0));
    end;

    if AColumnData.ColumnDescription.HasValue then
      LBuilder.Append(',').Append(AColumnData.ColumnDescription);

    LBuilder.Append(')');
    LBuilder.Append(']');

    Result := LBuilder.ToString;
  finally
    LBuilder.Free;
  end;
end;

function TDelphiUnitCodeGenerator.GetColumnProperties(AColumnData: TColumnData): string;
begin
  Result := '';
  if AColumnData.IsRequired then
    Result := Result + 'cpRequired,';

  if AColumnData.IsUnique then
    Result := Result + 'cpUnique,';

  if AColumnData.IsPrimaryKey then
    Result := Result + 'cpPrimaryKey,';

  if AColumnData.DontInsert then
    Result := Result + 'cpDontInsert,';

  if AColumnData.DontUpdate then
    Result := Result + 'cpDontUpdate,';

  if AColumnData.NotNull then
    Result := Result + 'cpNotNull,';

  if AColumnData.IsHidden then
    Result := Result + 'cpHidden,';

  if (Result <> '') then
  begin
    SetLength(Result, Length(Result)-1);
  end;

  Result := '[' + Result + ']';
end;

function TDelphiUnitCodeGenerator.GetColumnTypeName(AColumnData: TColumnData): string;
begin
  Result := AColumnData.ColumnTypeName;
  if FUseNullableTypes and not AColumnData.IsRequired then
  begin
    Result := 'Nullable<' + Result + '>';
  end;
end;

function TDelphiUnitCodeGenerator.GetPrivateFieldName(AColumnData: TColumnData): string;
begin
  Result := GetFielnamePrefix + AColumnData.ColumnName;
end;

function TDelphiUnitCodeGenerator.GetUnitName(AEntityData: TEntityModelData): string;
begin
  Result := FUnitPrefix + AEntityData.TableName;
end;

end.
