(*
* Copyright (c) 2012, Linas Naginionis
* Contacts: lnaginionis@gmail.com or support@soundvibe.net
* All rights reserved.
*
* Redistribution and use in source and binary forms, with or without
* modification, are permitted provided that the following conditions are met:
*     * Redistributions of source code must retain the above copyright
*       notice, this list of conditions and the following disclaimer.
*     * Redistributions in binary form must reproduce the above copyright
*       notice, this list of conditions and the following disclaimer in the
*       documentation and/or other materials provided with the distribution.
*     * Neither the name of the <organization> nor the
*       names of its contributors may be used to endorse or promote products
*       derived from this software without specific prior written permission.
*
* THIS SOFTWARE IS PROVIDED BY THE AUTHOR ''AS IS'' AND ANY
* EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
* WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
* DISCLAIMED. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY
* DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
* (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
* LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
* ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
* (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
* SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*)
unit SQL.Commands.Insert;

interface

uses
  SQL.AbstractCommandExecutor, Core.Interfaces, SQL.Types, SQL.Commands, SQL.Params, Generics.Collections
  , Mapping.Attributes;

type
  {$REGION 'Documentation'}
  ///	<summary>
  ///	  Responsible for building and executing <c>insert</c> statements.
  ///	</summary>
  {$ENDREGION}
  TInsertExecutor = class(TAbstractCommandExecutor)
  private
    FTable: TSQLTable;
    FCommand: TInsertCommand;
    FColumns: TList<ColumnAttribute>;
    FAutoGeneratedName: string;
    FLastInsertIdSQL: string;
    FGetSequenceValueSQL: string;
    FPrimaryKeyColumn: ColumnAttribute;
  protected
    function CanGetIdentity(): Boolean; virtual;
    function CanGetSequenceValue(): Boolean; virtual;
    function GetCommand: TDMLCommand; override;
  public
    constructor Create(); override;
    destructor Destroy; override;

    procedure BuildParams(AEntity: TObject); override;

    procedure Build(AClass: TClass); override;
    procedure Execute(AEntity: TObject); override;
    procedure LoadIdFromSequence(AEntity: TObject; AResultset: IDBResultset);


    property SQLTable: TSQLTable read FTable;
  end;

implementation

uses
  Core.Exceptions
  ,Core.EntityCache
  ,Mapping.RttiExplorer
  ,SysUtils
  ,Rtti
  ;

{ TInsertCommand }

procedure TInsertExecutor.Build(AClass: TClass);
var
  LAtrTable: TableAttribute;
  LEntityData: TEntityData;
begin
  EntityClass := AClass;
  LEntityData := TEntityCache.Get(AClass);
  LAtrTable := LEntityData.EntityTable;
  if not Assigned(LAtrTable) then
    raise ETableNotSpecified.CreateFmt('Table not specified for class "%S"', [AClass.ClassName]);

  FTable.SetFromAttribute(LAtrTable);

  FColumns.Clear;
  FColumns.AddRange(LEntityData.Columns);

  FPrimaryKeyColumn := LEntityData.PrimaryKeyColumn;
  FAutoGeneratedName := TRttiExplorer.GetAutoGeneratedColumnMemberName(EntityClass);
   //add fields to tsqltable
  FCommand.Sequence := LEntityData.Sequence;
  FCommand.SetTable(FColumns);

  if LEntityData.HasSequence then
  begin
    if (LEntityData.Sequence.SeqSQL <> '') then
      FGetSequenceValueSQL := LEntityData.Sequence.SeqSQL
    else
      FGetSequenceValueSQL := Generator.GenerateGetNextSequenceValue(LEntityData.Sequence);

    if (FGetSequenceValueSQL <> '') then
      FCommand.InsertFields.Add(TSQLField.Create(LEntityData.PrimaryKeyColumn.Name, FTable));
  end;

  SQL := Generator.GenerateInsert(FCommand);

  if LEntityData.PrimaryKeyColumn.IsIdentity then
    FLastInsertIdSQL := Generator.GenerateGetLastInsertId(LEntityData.PrimaryKeyColumn);
end;

procedure TInsertExecutor.BuildParams(AEntity: TObject);
var
  LParam: TDBParam;
  LColumn: ColumnAttribute;
begin
  inherited BuildParams(AEntity);
  FCommand.Entity := AEntity;
  
  for LColumn in FColumns do
  begin
    if not LColumn.IsIdentity {SameText(FAutoGeneratedName, LColumn.ClassMemberName)} and (LColumn.CanInsert) then
    begin
      LParam := CreateParam(AEntity, LColumn);
      SQLParameters.Add(LParam);
    end;
  end;
end;

function TInsertExecutor.CanGetIdentity: Boolean;
begin
  Result := (FLastInsertIdSQL <> '');
end;

function TInsertExecutor.CanGetSequenceValue: Boolean;
begin
  Result := (FGetSequenceValueSQL <> '');
end;

constructor TInsertExecutor.Create();
begin
  inherited Create();
  FTable := TSQLTable.Create;
  FCommand := TInsertCommand.Create(FTable);
  FColumns := TList<ColumnAttribute>.Create;
  FAutoGeneratedName := '';
  FLastInsertIdSQL := '';
  FGetSequenceValueSQL := '';
  FPrimaryKeyColumn := nil;
end;

destructor TInsertExecutor.Destroy;
begin
  FTable.Free;
  FCommand.Free;
  FColumns.Free;

  inherited Destroy;
end;

procedure TInsertExecutor.Execute(AEntity: TObject);
var
  LStmt: IDBStatement;
  LSequenceValue: Variant;
  LResultset: IDBResultset;
begin
  Assert(Assigned(AEntity));

  LStmt := Connection.CreateStatement;

  if CanGetSequenceValue then
  begin
    //retrieve identity value from the sequence
    LStmt.SetSQLCommand(FGetSequenceValueSQL);
    LSequenceValue := LStmt.ExecuteQuery.GetFieldValue(0);
  end;

  if CanGetIdentity then
    SQL := SQL + #13#10 +  ' ' + FLastInsertIdSQL;

  LStmt.SetSQLCommand(SQL);

  BuildParams(AEntity);

  if CanGetSequenceValue then
  begin
    //add sequence param value
    SQLParameters.Add(DoCreateParam(FPrimaryKeyColumn, LSequenceValue));
  end;

  try
    LStmt.SetParams(SQLParameters);

    if (SQL = '') then
    begin
      SQL := Generator.GenerateInsert(FCommand);
      LStmt.SetSQLCommand(SQL);
    end;


    inherited Execute(AEntity);
    if not CanGetIdentity then
      LStmt.Execute()
    else  //we must use client side cursor because ADO incorrectly fetches identity value with server side cursor
      LResultset := LStmt.ExecuteQuery(False);

    LoadIdFromSequence(AEntity, LResultset);

    if CanGetSequenceValue then
    begin
      TRttiExplorer.SetMemberValue(nil, AEntity, FPrimaryKeyColumn, TValue.FromVariant(LSequenceValue));
    end;

  finally
    LStmt := nil;
    LResultset := nil;
  end;
end;

function TInsertExecutor.GetCommand: TDMLCommand;
begin
  Result := FCommand;
end;

procedure TInsertExecutor.LoadIdFromSequence(AEntity: TObject; AResultset: IDBResultset);
var
  LID: Variant;
  LStmt: IDBStatement;
  LResultset: IDBResultset;
begin
  if not CanGetIdentity then
    Exit;

  LResultset := AResultset;

  if LResultset.IsEmpty then
  begin
    LStmt := Connection.CreateStatement;
    LStmt.SetSQLCommand(FLastInsertIdSQL);
    LResultset := LStmt.ExecuteQuery();
    if LResultset.IsEmpty then
      Exit;
  end;

  LID := LResultset.GetFieldValue(0);
  TRttiExplorer.SetMemberValue(nil, AEntity, FAutoGeneratedName, TValue.FromVariant(LID));
end;

end.
